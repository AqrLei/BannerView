apply plugin: 'maven-publish'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'signing'

dokka {
    outputFormat = "javadoc"
    outputDirectory = "$buildDir/documentation"
    //配置方式一
    configuration {
        noJdkLink = true
        noStdlibLink = true
        noAndroidSdkLink = true
        skipDeprecated = true // Do not output deprecated members
        reportUndocumented = true // Emit warnings about not documented members.
        skipEmptyPackages = true // Do not create index pages for empty packages
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.source
    archiveClassifier.set('sources')
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    from dokka.outputDirectory
    archiveClassifier.set('javadoc')
}

tasks.withType(Javadoc) {//防止编码问题
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId REPO_GROUP_NAME
                artifactId ARTIFACT_NAME
                version VERSION_NAME

                artifact(sourcesJar)
                artifact(dokkaJar)

                pom {
                    packaging 'aar'
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'AqrLei'
                            name = 'AqrLei'
                            email = 'aqrdeveloper@gmail.com'
                        }
                    }
                }
            }

        }

        repositories {
            maven {
                url = "$rootProject.rootDir/repo"
            }
        }
    }
    signing {
        sign publishing.publications.release
    }
}